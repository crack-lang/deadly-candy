## A CSV Viewer.  Views a single CSV file.
##
## Use the up and down keys to navigate lines, left and right to shift the
## view to columns that spill over to the edge.
##
## Use the escape key to terminate.
##
## This application assumes that the first line of the CSV file is the column
## headers, a practice that seems to be common for CSV files.

# Copyright 2020 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import crack.ascii rstrip;
import crack.exp.termui Event, KeyEvent, TermApp;
import crack.fs makePath;
import crack.io cerr;
import crack.io.readers LineReader;
import crack.lang AppendBuffer, IndexError;
import crack.sys argv;
import crack.enc.csv parseLine;
import .entry Entry;
import .layout Column, EXPAND;
import .table TableModel, Table;

const KEY_F3 := '\033OR';

class MyApp : TermApp {
    void onEvent(Event event) {
        if ((ke := KeyEvent.cast(event, null)) && ke.data == KEY_F3)
            terminate();
        else
            TermApp.onEvent(event);
    }
}

# Load the CSV.
model := TableModel();
try {
    src := LineReader(makePath(argv[1]).reader());
    for (line :in src)
        model.append(parseLine(rstrip(line)));
} catch (IndexError ex) {
    cerr I`Usage: csview <csv-file>
           CSV File viewer.  Use F3 to quit, arrows to navigate.
           `;
}

# Start the UI.
app := MyApp();
app.start();

column := Column(app);
column.addChild(table := Table(column, model), EXPAND);
table.showHeaders = true;
buf := AppendBuffer(256);
column.addChild(Entry(column, buf), 1);

app.showCursor(false);
table.setFocus(true);

app.run();
app.showCursor(true);
app.stop();