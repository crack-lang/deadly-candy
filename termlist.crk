# Copyright 2020 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import crack.cont.array Array;
import crack.cont.hashmap HashMap;
import crack.exp.termui Event, KeyEvent, TermApp, Window, KEY_ENTER, KEY_ESC,
    KEY_DOWN, KEY_PGDN, KEY_PGUP, KEY_UP;
import crack.functor Functor2;

import .util convertKey;

class List;
alias EventCallback = Functor2[bool, List, Event];

@export_symbols EventCallback;

class List : Window {

    Array[String] model;

    ## Top line index.
    uint top = 0;

    ## Selected line index.
    uint selection;

    HashMap[int, EventCallback] __bindings = {};

    byte
        normalAttr = 6,  # cyan
        selectionAttr = 0x47, # white on blue
        matchAttr = 0x43, # yellow on blue
        unfocusedSelectionAttr = 0x86; # Underlined, cyan

    oper init(TermApp app, Array[String] model) : Window(app), model = model {
        size := app.getSize();
        width = size.x;
        height = size.y;
    }

    oper init(Window parent, Array[String] model) :
        Window(parent),
        model = model {
    }

    void bind(int key, EventCallback action) {
        __bindings[key] = action;
    }

    ## Show the selection at the specified index.
    void show(uint index) {
        if (index > model.count())
            return;

        if (index < top)
            top = index;
        else if (index - top >= height)
            top = index - height + 1;
    }

    bool onEvent(Event event) {
        if (event := KeyEvent.cast(event, null)) {

            if (handler := __bindings.get(convertKey(event)))
                return handler(this, event);

            if (event.key == KEY_UP && selection > 0) {
                --selection;
                if (selection < top)
                    top = selection;
            } else if (event.key == KEY_DOWN &&
                       selection < model.count() - 1) {
                ++selection;
                if (selection - top >= height)
                    top = selection - height + 1;
            } else if (event.key == KEY_PGUP) {
                if (top < height) {
                    # Go to the top, leave the selection the same.
                    top = 0;
                } else {
                    # Go one page up, move selection one page up.
                    top = top - height;
                    if (selection >= height)
                        selection -= height;
                }
            } else if (event.key == KEY_PGDN) {
                if (top >= model.count() - height) {
                    top = model.count() - height;
                } else {
                    top += height;
                    if (selection < model.count() - height)
                        selection += height;
                }
            } else {
                return false;
            }
        }
        return true;
    }

    void paint() {
        gc := getGraphicsContext();
        gc.fillRect(0, 0, x, y, 0, b' ');
        cur := model.iter();
        cur.index = top;
        focused := getFocus();
        for (int y = 0; cur && y - top < height; ++y) {
            selected := y + top == selection;
            if (focused && y + top == selection)
                gc.write(0, y, selectionAttr, cur.elem());
            else if (selected)
                gc.write(0, y, unfocusedSelectionAttr, cur.elem());
            else
                gc.write(0, y, normalAttr, cur.elem());
            cur.next();
        }
    }
}

