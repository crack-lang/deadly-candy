## Entryfield control.

import crack.cont.hashmap HashMap;
import crack.exp.termui Event, KeyEvent, TermApp, Window, KEY_BACKSPACE,
    KEY_LEFT, KEY_RIGHT;
import crack.functor Functor1, Functor2;
import crack.lang AppendBuffer, Buffer;

class Entry;

alias EventCallback = Functor2[bool, Entry, Event];

# Called when an event changes the model.
alias ChangeCallback = Functor1[void, AppendBuffer];

@export_symbols EventCallback;

## A one line entryfield.
class Entry : Window {

    AppendBuffer model;
    byte normalAttr = 6,   # Cyan
        cursorAttr = 0x46; # Cyan on blue.

    ## Index of the first visible character.
    uintz start;

    ## Cursor position.
    uintz pos;

    ## Called when an event changes the contents of the model object.
    ChangeCallback changeCallback;

    HashMap[int, EventCallback] __bindings = {};

    oper init(Window parent, AppendBuffer model) :
        Window(parent),
        model = model {
    }

    oper init(Window parent, String origValue) :
        Window(parent),
        model(origValue.size) {

        model.extend(origValue);
    }

    void bind(int key, EventCallback action) {
        __bindings[key] = action;
    }

    @final void __syncToModel() {
        if (start > model.size)
            start = 0;
        if (pos > model.size)
            pos = model.size;
    }

    bool onEvent(Event event) {
        __syncToModel();
        if (event := KeyEvent.cast(event, null)) {
            if ((handler := __bindings.get(event.key)) &&
                handler(this, event)
                )
                return true;

            if (event.key == KEY_BACKSPACE || event.key == 127) {
                if (!pos)
                    return true;

                model.move(pos - 1, pos, model.size - pos);
                --model.size;
                --pos;
                if (changeCallback) changeCallback(model);
                return true;
            }

            if (event.key == KEY_LEFT) {
                if (pos > 0)
                    --pos;
                if (pos < start)
                    start = pos;
                return true;
            }

            if (event.key == KEY_RIGHT) {
                if (pos < model.size)
                    ++pos;
                if (pos - start > width)
                    pos = start + width - 1;
                return true;
            }

            # Any other non-ascii's get delegated.
            if (event.key < 32 || event.key > 127)
                return getParent().onEvent(event);

            # Otherwise just add the character.
            if (pos < model.size) {
                if (model.cap < model.size + 1)
                    model.grow(model.cap * 2);
                model.move(pos + 1, pos, model.size - pos);
                model.buffer[pos] = byte(event.key);
            } else {
                model.append(byte(event.key));
            }
            if (changeCallback) changeCallback(model);
            ++pos;
        }

        return true;
    }

    void paint() {
        __syncToModel();
        gc := getGraphicsContext();
        gc.fillRect(0, 0, width, height, normalAttr, b' ');
        gc.write(0, 0, normalAttr,
                 String(model, start, model.size - start)
                 );

        # Adjust the cursor.
        if (getFocus()) {
            if (pos == model.size)
                gc.write(pos - start, 0, cursorAttr, ' ');
            else
                gc.write(pos - start, 0, cursorAttr,
                         String(1, model.buffer[pos])
                         );
        }
    }
}